#Το πρώτο query εφαρμόζεται  με σκοπό τον περιορισμό τoυ θορύβου,
#εντοπίζοντας την χρονική αστάθεια ("αναλαμπές") σε πολύγωνα
#καμένων εκτάσεων. Υλοποιήσαμε και εφαρμόσαμε το ακόλουθο  χρονικό φίλτρο
#στα δεδομένα (με αναφορές στον πηγαίο κώδικα):
#- Στις γραμμές 1574..1614, για κάθε πολύγωνο, ομαδοποιούμε τις
#καταγραφές οι οποίες δεν απέχουν μεταξύ τους (επόμενη-προηγούμενη)
#περισσότερο από την τιμή της μεταβλητής persistence.
#- Στην συνέχεια εξετάζουμε κάθε τέτοια ομάδα (κοντινών στο χώρο
#καταγραφών), και ελέγχουμε εάν το πλήθος τους (για κάθε τέτοια ομάδα)
#είναι μικρότερο ή ίσο από την τιμή της μεταβλητής repeat_in_persistence
#(γραμμές 1614..1630).
#- Αυτές τις καταγραφές τις διαγράφουμε θεωρώντας τις θόρυβο

# ../../scripts/strabon -db testNOANewRefinements query  "`./instantiate.sh -s MSG2 -c StaticThresholds  -m '2012-08-24T12:00:00' -M '2012-08-24T12:40:00' -p 10 -r 3 discoverFires-start.rq | grep -v '#[[:alpha:]]' | grep -v '#[ \(-]' | sed 's/"/\\\\"/g'`" TSV

PREFIX noa: <http://teleios.di.uoa.gr/ontologies/noaOntology.owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX strdf: <http://strdf.di.uoa.gr/ontology#>
PREFIX strdf-ext: <http://strdf.di.uoa.gr/extensions/ontology#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX gag: <http://geo.linkedopendata.gr/greekadministrativeregion/ontology#>

SELECT (substr(str(?hStartTime), 12, 5) as ?start) (substr(str(max(?hTime)), 12, 5) as ?end) ?geo
WHERE {
	# -- FIND HOTSPOTS THAT DEFINE THE START OF A FIRE (GROUP ALA THEMOS) -- 
	# Retrieve all hotstpots in time range...  
	?hStart	noa:hasAcquisitionTime ?hStartTime ;
            noa:hasGeometry ?geo ;
			noa:producedFromProcessingChain "PROCESSING_CHAIN"^^xsd:string ;
			noa:isDerivedFromSensor "SENSOR"^^xsd:string .
	FILTER("MIN_ACQUISITION_TIME"^^xsd:dateTime <= ?hStartTime && ?hStartTime <= "MAX_ACQUISITION_TIME"^^xsd:dateTime).
	# ... narrow down results so that any previous hotspots is detected at least PERSISTENCE mins earlier
    OPTIONAL {
        ?hPrevious	noa:hasAcquisitionTime ?hPreviousTime ;
        			noa:hasGeometry ?geo ;
					noa:producedFromProcessingChain "PROCESSING_CHAIN"^^xsd:string ;
					noa:isDerivedFromSensor "SENSOR"^^xsd:string .
		FILTER("MIN_ACQUISITION_TIME"^^xsd:dateTime <= ?hStartTime && ?hStartTime <= "MAX_ACQUISITION_TIME"^^xsd:dateTime).
        FILTER(strdf-ext:diffDateTime(?hStartTime, ?hPreviousTime) <= PERSISTENCE*60000 && ?hStartTime > ?hPreviousTime).
    } 
    FILTER(!bound(?hPrevious)) .

    # FIND HOTSPOTS AFTER A FIRE STARTING TIME
    ?h  noa:hasAcquisitionTime ?hTime;
        noa:hasGeometry ?hGeo;
		noa:producedFromProcessingChain "PROCESSING_CHAIN"^^xsd:string ;
		noa:isDerivedFromSensor "SENSOR"^^xsd:string .
	FILTER("MIN_ACQUISITION_TIME"^^xsd:dateTime <= ?hStartTime && ?hStartTime <= "MAX_ACQUISITION_TIME"^^xsd:dateTime).
    FILTER(?hTime >= ?hStartTime).

    # OPTIONALLY FIND ANOTHER STARTING TIME BEFORE ?h and after ?hStart
    OPTIONAL {
        ?hStart2	noa:hasAcquisitionTime ?hStartTime2 ;
   					noa:hasGeometry ?geo ;		
					noa:producedFromProcessingChain "PROCESSING_CHAIN"^^xsd:string ;
					noa:isDerivedFromSensor "SENSOR"^^xsd:string .
		FILTER("MIN_ACQUISITION_TIME"^^xsd:dateTime <= ?hStartTime2 && ?hStartTime2 <= "MAX_ACQUISITION_TIME"^^xsd:dateTime).
        OPTIONAL {                        		
            ?hPrevious2	noa:hasAcquisitionTime ?hPreviousTime2 ;
            			noa:hasGeometry ?geo ;
						noa:producedFromProcessingChain "PROCESSING_CHAIN"^^xsd:string ;
						noa:isDerivedFromSensor "SENSOR"^^xsd:string .
			FILTER("MIN_ACQUISITION_TIME"^^xsd:dateTime <= ?hStartTime2 && ?hStartTime2 <= "MAX_ACQUISITION_TIME"^^xsd:dateTime).
            FILTER( strdf-ext:diffDateTime(?hStartTime2, ?hPreviousTime2) <= PERSISTENCE*60000 && ?hStartTime2 > ?hPreviousTime2).
        } 
        FILTER(!bound(?hPrevious2)) .
        FILTER(?hStartTime < ?hStartTime2 && ?hStartTime2 <= ?hTime).
    }    
    FILTER(!bound(?hStart2))
}
GROUP BY ?hStartTime ?geo 
HAVING (count(?h) >= REPEAT_IN_PERS)
